import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version "${spring_boot}" apply(false)
    id 'io.spring.dependency-management' version "${io_spring_dependency_management}" apply(false)
}

wrapper {
    gradleVersion = "8.5"
}

static boolean isSpringBootModule(project) {
    return project.name.endsWith("-server")
}


group 'cn.monkey'
version '1.0-SNAPSHOT'

subprojects {
    project ->

        apply plugin: 'io.spring.dependency-management'

        if (isSpringBootModule(project)) {
            apply plugin: 'org.springframework.boot'
        }

        apply plugin: 'java'
        apply plugin: 'idea'
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

        repositories {
            //mavenLocal()
            maven {
                url "https://maven.aliyun.com/repository/public/"
            }
            maven {
                url "https://maven.aliyun.com/repository/spring/"
            }
            mavenCentral()
        }


        dependencyManagement {
            imports {
                mavenBom SpringBootPlugin.BOM_COORDINATES
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud}"
                mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${spring_cloud_alibaba}"
            }
        }
}